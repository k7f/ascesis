# cesar syntax in EBNF

# White space is optional anywhere between tokens, except inside
# identifiers and literals.

ces_file = { ces_def | ces_inst | meta_block } ;

## Structure definition

ces_def = ces_sig rex ;
ces_sig = "ces" ces_ident [ "(" [ arg_list_decl ] ")" ] ;

## Structure instantiation

ces_inst = ces_ident "(" [ arg_list ] ")" ;

## Context

meta_block = nodes_block ; # | ...
nodes_block = nodes_sig "{" node_spec { "," node_spec } [ "," ] "}" ;
nodes_sig = "nodes" [ "(" [ arg_list_decl ] ")" ] ;
node_spec = node_ident ":" "{" node_attr { "," node_attr } [ "," ] "}" ;
node_attr = name_spec | cap_spec ;
name_spec = "name" ":" name_expr ;
name_expr = string ; # | ...
cap_spec = "cap" ":" cap_expr ;
cap_expr = size ; # | ...

## Argument declarations and values

arg_list_decl = arg_spec { "," arg_spec } [ "," ] ;
arg_spec = arg_ident ":" ( "Node" | "CES" | "Size" | "String" ) ;
arg_list = arg_value { ","  arg_value } [ "," ] ;
arg_value = node_ident | ces_ident | size | string ;

## Rule expressions

rex = "{" rule "}"
    | "{" rex { [ "+" ] rex } "}" ;

## Rules

rule = thin_rule | fat_rule ;

thin_rule = e_rule | c_rule | ec_rule | ce_rule | fw_rule | bw_rule ;

# effect polynomial with explicit node list on the left
e_rule = node_list "->" polynomial ;

# cause polynomial with explicit node list on the left
c_rule = node_list "<-" polynomial ;

# effect-then-cause polynomial with explicit node list on the left
ec_rule = node_list "->" polynomial "<-" polynomial ;

# cause-then-effect polynomial with explicit node list on the left
ce_rule = node_list "<-" polynomial "->" polynomial ;

# cause-then-effect pair of polynomials with explicit node list in the
# middle
fw_rule = "+" plain_polynomial "->" node_list "->" polynomial ;

# effect-then-cause pair of polynomials with explicit node list in the
# middle
bw_rule = "+" plain_polynomial "<-" node_list "<-" polynomial ;

node_list = node_ident { "," node_ident } [ "," ] ;

# multi-polynomial rule with implicit node lists
fat_rule = polynomial ( "=>" | "<=" ) polynomial { ( "=>" | "<=" ) polynomial } ;

## Polynomial

polynomial = [ "+" ] plain_polynomial ;
plain_polynomial = [ plain_polynomial "+" ] poly_term { poly_term } ;
poly_term = node_ident | "(" polynomial ")" ;

## Identifiers

# FIXME whitespace
ces_ident = uppercase_letter { alphanum_char } ;
node_ident = lowercase_letter { alphanum_char } ;
arg_ident = ( "_" | letter ) { alphanum_char } ;

## Literals

# FIXME whitespace
size = digit { digit } ;
string = "\"" { any_char } "\"" ;

## Character classes

alphanum_char = letter | digit | "_" ;
letter = uppercase_letter | lowercase_letter ;
uppercase_letter = "A".."Z" ;
lowercase_letter = "a".."z" ;
digit = "0".."9" ;
any_char = plain_char | escaped_char ;
plain_char = "\x20".."\x7F" | "\x80".."\xFF" ;
escaped_char = "\\" ( "\"" | "\\" | "a" | "b" | "n" | "r" | "t" ) ;
