# Cesar syntax in EBNF

# Some aspects of the language are described informally in other
# files: lexer-implementation.md, parser-implementation.md.

ces_file = {
           ces_immediate_def
         | ces_template_def | context_block } ;

## Structure definition

ces_immediate_def = ces_immediate_sig rex ;
ces_immediate_sig = "ces" identifier ;

ces_template_def = ces_template_sig rex ;

# empty argument list is disallowed
ces_template_sig = "ces" identifier "(" arg_list_decl ")" ;

## Structure instantiation

# empty argument list is disallowed
ces_template_inst = identifier "(" arg_list ")" ;

## Context

context_block = lab_block | cap_block | mul_block | inh_block ;
lab_block = "lab" "{" lab_spec { "," lab_spec } [ "," ] "}" ;
lab_spec = identifier ":" string ;
cap_block = "cap" "{" cap_spec { "," cap_spec } [ "," ] "}" ;
cap_spec = size monomial ;
mul_block = "mul" "{" mul_spec { "," mul_spec } [ "," ] "}" ;
mul_spec = size monomial ( "->" | "<-" ) monomial ;
inh_block = "inh" "{" inh_spec { "," inh_spec } [ "," ] "}" ;
inh_spec = monomial ( "->" | "<-" ) monomial ;
monomial = identifier | { identifier } ;

## Argument list

arg_list_decl = arg_spec { "," arg_spec } [ "," ] ;
arg_spec = identifier ":" ( "Node" | "CES" | "Size" | "String" ) ;
arg_list = arg_value { ","  arg_value } [ "," ] ;
arg_value = identifier | size | string ;

## Rule expression

rex = thin_arrow_rule
    | fat_arrow_rule
    | rex_term { [ "+" ] rex_term } ;

rex_term = ces_instantiation | "{" rex "}" ;

## Arrow rules

thin_arrow_rule = e_rule | c_rule | ec_rule | ce_rule | fw_rule | bw_rule ;

# effect polynomial with explicit node list on the left
e_rule = node_list "->" polynomial ;

# cause polynomial with explicit node list on the left
c_rule = node_list "<-" polynomial ;

# effect-then-cause polynomial with explicit node list on the left
ec_rule = node_list "->" polynomial "<-" polynomial ;

# cause-then-effect polynomial with explicit node list on the left
ce_rule = node_list "<-" polynomial "->" polynomial ;

# cause-then-effect pair of polynomials with explicit node list in the
# middle
fw_rule = "+" plain_polynomial "->" node_list "->" polynomial ;

# effect-then-cause pair of polynomials with explicit node list in the
# middle
bw_rule = "+" plain_polynomial "<-" node_list "<-" polynomial ;

node_list = identifier { ","  | identifier } [ "," ] ;

# multi-polynomial rule with implicit node lists
fat_arrow_rule = polynomial ( "=>" | "<=" ) polynomial { ( "=>" | "<=" ) polynomial } ;

## Polynomial

polynomial = [ "+" ] plain_polynomial ;
plain_polynomial = [ plain_polynomial "+" ] poly_term { poly_term } ;
poly_term = identifier | "(" polynomial ")" ;
