use lalrpop_util::ErrorRecovery;
use crate::{
    CesFile, CesFileBlock, CesName, ImmediateDef, CesImmediate, CesInstance,
    PropBlock, PropValue, CapacitiesBlock, UnboundedBlock, WeightsBlock,
    InhibitorsBlock, WeightlessBlock, Rex, ThinArrowRule, FatArrowRule, Polynomial,
    DotName, BinOp, Literal, Token, AscesisError, AscesisErrorKind,
};

grammar<'input, 'env>(errors: &'env mut Vec<ErrorRecovery<usize, Token<'input>, AscesisError>>);

// ces_file = { immediate_def | template_def | context_block } ;
pub CesFile: CesFile = <CesFileBlock*> => <>.into();

pub CesFileBlock: CesFileBlock = {
    <ImmediateDef> => <>.into(),
    <PropBlock> => <>.into(),
    <CapsBlock> => <>.into(),
    <UnboundedBlock> => <>.into(),
    <WeightsBlock> => <>.into(),
    <InhibitBlock> => <>.into(),
    <WeightlessBlock> => <>.into(),
    ! => {
        errors.push(<>);
        CesFileBlock::Bad(AscesisErrorKind::ParsingFailure.into())
    },
};

// Structure definition, immediate form

// immediate_def = immediate_sig "{" [ rex ] "}" ;
pub ImmediateDef: ImmediateDef = {
    <sig: ImmediateSig> "{" "}" =>
        ImmediateDef::new(sig, Rex::new()),
    <sig: ImmediateSig> "{" <rex: Rex> "}" =>
        ImmediateDef::new(sig, rex),
};

// immediate_sig = "ces" identifier ;
ImmediateSig: CesName =
    "ces" <Identifier> =>
        CesName::from(<>);

// Structure definition, template form

// FIXME
// template_def = template_sig "{" [ rex ] "}" ;
// template_sig = "ces" identifier "(" { template_args } ")" ;
// template_args = arg_decl { "," arg_decl } [ "," ] ;
// arg_decl = identifier ":" ( "Dot" | "CES" | "Size" | "String" ) ;

// Structure instantiation

// ces_immediate = identifier "(" ")" ;
pub CesImmediate: CesImmediate =
    <name: Identifier> "(" ")" =>
        CesImmediate::new(CesName::from(name));

// ces_instance = identifier "!" "(" [ instance_args ] ")" ;
pub CesInstance: CesInstance = {
    <name: Identifier> "!" "(" ")" =>
        CesInstance::new(CesName::from(name)),
    <name: Identifier> "!" "(" <args: InstanceArgs> ")" =>
        CesInstance::new(CesName::from(name)).with_args(args),
};

// instance_args = arg_value { ","  arg_value } [ "," ] ;
InstanceArgs: Vec<String> = {
    <head: ArgValue> <tail: CommaThenArgValue*> ","? => {
        let mut tail = tail;
        let mut result = vec![head];
        result.append(&mut tail);
        result
    }
};

CommaThenArgValue: String = "," <ArgValue>;

// arg_value = identifier | size | string ;
ArgValue: String = <Identifier>;

// Context

// context_block = prop_block
//               | caps_block
//               | unbounded_block
//               | weights_block
//               | inhibit_block
//               | activate_block
//               | drop_block ;

// prop_selector = "vis" | "sat" ;
PropSelector: String = {
    "vis" => <>.into(),
    "sat" => <>.into(),
};

// prop_block = prop_selector "{" [ prop_list ] "}" ;
pub PropBlock: PropBlock = {
    <sel: PropSelector> "{" "}" =>
        PropBlock::new().with_selector(sel),
    <sel: PropSelector> "{" <blk: PropList> "}" =>
        blk.with_selector(sel),
};

// prop_list = prop_field { "," prop_field } [ "," ] ;
PropList: PropBlock =
    <head: PropField> <tail: CommaThenPropField*> ","? =>
        head.with_more(tail);

CommaThenPropField: PropBlock = "," <PropField>;

// prop_field = identifier ":" prop_value ;
PropField: PropBlock =
    <key: Identifier> ":" <value: PropValue> =>
        PropBlock::new().with_prop(key, value);

// prop_value_array = prop_value { "," prop_value } [ "," ] ;
PropValueArray: PropValue =
    <head: PropValue> <tail: CommaThenPropValue*> ","? =>
        head.into_array_with_more(tail);

CommaThenPropValue: PropValue = "," <PropValue>;

// prop_value = name
//            | size_list
//            | dot_list
//            | "[" prop_value_array "]"
//            | "{" prop_list "}" ;
PropValue: PropValue = {
    <Name> =>? Ok(PropValue::new_name(<>)?),
    <SizeList> =>? Ok(PropValue::new_size_list(<>)?),
    <IdentifierList> =>? Ok(PropValue::new_dot_list(<>)?),
    "[" <PropValueArray> "]" => <>.into(),
    "{" <PropList> "}" => <>.into(),
};

// caps_block = "caps" "{" [ cap_list ] "}" ;
pub CapsBlock: CapacitiesBlock = {
    "caps" "{" "}" => CapacitiesBlock::new(),
    "caps" "{" <head: CapField> <tail: CommaThenCapField*> ","? "}" =>
        head.with_more(tail),
};

// cap_list = cap_field { "," cap_field } [ "," ] ;
CommaThenCapField: CapacitiesBlock = "," <CapField>;

// cap_field = size dot_list ;
CapField: CapacitiesBlock =
    <size: Size> <dots: DotList> =>?
        Ok(CapacitiesBlock::new().with_dot_names(size, dots)?);

// unbounded_block = "unbounded" "{" [ dot_list ] "}" ;
pub UnboundedBlock: UnboundedBlock = {
    "unbounded" "{" "}" => UnboundedBlock::new(),
    "unbounded" "{" <dots: DotList> "}" =>?
        Ok(UnboundedBlock::from_dot_names(dots)?),
};

// weights_block = "weights" "{" [ weight_list ] "}" ;
pub WeightsBlock: WeightsBlock = {
    "weights" "{" "}" => WeightsBlock::new(),
    "weights" "{" <head: WeightField> <tail: CommaThenWeightField*> ","? "}" =>
        head.with_more(tail),
};

// weight_list = weight_field { "," weight_field } [ "," ] ;
CommaThenWeightField: WeightsBlock = "," <WeightField>;

// weight_field = size dot_list ( "->" | "<-" ) dot_list ;
WeightField: WeightsBlock = {
    <size: Size> <pre_dots: DotList> "->" <post_set: DotList> =>?
        Ok(WeightsBlock::new_fork_weights(size, pre_dots, post_set)?),
    <size: Size> <post_dots: DotList> "<-" <pre_set: DotList> =>?
        Ok(WeightsBlock::new_join_weights(size, post_dots, pre_set)?),
};

// inhibit_block = "inhibit" "{" [ inhibit_list ] "}" ;
pub InhibitBlock: InhibitorsBlock = {
    "inhibit" "{" "}" => InhibitorsBlock::new(),
    "inhibit" "{" <head: InhibitField> <tail: CommaThenInhibitField*> ","? "}" =>
        head.with_more(tail),
};

// inhibit_list = inhibit_field { "," inhibit_field } [ "," ] ;
CommaThenInhibitField: InhibitorsBlock = "," <InhibitField>;

// inhibit_field = dot_list ( "->" | "<-" ) dot_list ;
InhibitField: InhibitorsBlock = {
    <pre_dots: DotList> "->" <post_poly: Polynomial> =>?
        Ok(InhibitorsBlock::new_effects(pre_dots, post_poly)?),
    <post_dots: DotList> "<-" <pre_poly: DotList> =>?
        Ok(InhibitorsBlock::new_causes(post_dots, pre_poly)?),
};

pub WeightlessBlock: WeightlessBlock = { ActivateBlock, DropBlock };

// Activate_block = "activate" "{" [ activate_list ] "}" ;
pub ActivateBlock: WeightlessBlock = {
    "activate" "{" "}" => WeightlessBlock::new(),
    "activate" "{" <head: ActivateField> <tail: CommaThenActivateField*> ","? "}" =>
        head.with_more(tail),
};

// activate_list = activate_field { "," activate_field } [ "," ] ;
CommaThenActivateField: WeightlessBlock = "," <ActivateField>;

// activate_field = dot_list "->" dot_list ;
ActivateField: WeightlessBlock =
    <pre_dots: DotList> "->" <post_poly: Polynomial> =>?
        Ok(WeightlessBlock::new_effects(pre_dots, post_poly)?);

// drop_block = "drop" "{" [ drop_list ] "}" ;
pub DropBlock: WeightlessBlock = {
    "drop" "{" "}" => WeightlessBlock::new(),
    "drop" "{" <head: DropField> <tail: CommaThenDropField*> ","? "}" =>
        head.with_more(tail),
};

// drop_list = drop_field { "," drop_field } [ "," ] ;
CommaThenDropField: WeightlessBlock = "," <DropField>;

// drop_field = dot_list "<-" dot_list ;
DropField: WeightlessBlock =
    <post_dots: DotList> "<-" <pre_poly: Polynomial> =>?
        Ok(WeightlessBlock::new_causes(post_dots, pre_poly)?);

// Rule expression

// rex = thin_arrow_rule
//     | fat_arrow_rule
//     | rex_term { [ "+" ] rex_term } ;
pub Rex: Rex = {
    <ThinArrowRule> => <>.into(),
    <FatArrowRule> => <>.into(),
    <head: RexTerm> <tail: (<AddOp?> <RexTerm>)*> => head.with_more(tail),
};

// rex_term = ces_immediate | ces_instance | "{" rex "}" ;
RexTerm: Rex = {
    <CesImmediate> => <>.into(),
    <CesInstance> => <>.into(),
    "{" <Rex> "}",
};

// Arrow rules

// thin_arrow_rule = e_rule | c_rule | ec_rule | ce_rule | fw_rule | bw_rule ;
// e_rule = dot_list "->" polynomial ;
// c_rule = dot_list "<-" polynomial ;
// ec_rule = dot_list "->" polynomial "<-" polynomial ;
// ce_rule = dot_list "<-" polynomial "->" polynomial ;
// fw_rule = "+" plain_polynomial "->" dot_list "->" polynomial ;
// bw_rule = "+" plain_polynomial "<-" dot_list "<-" polynomial ;
pub ThinArrowRule: ThinArrowRule = {
    // effect-only rule
    <dots: Polynomial> "->" <effect: Polynomial> =>?
        Ok(ThinArrowRule::new().with_dots(dots)?.with_effect(effect)),

   // cause-only rule
    <dots: Polynomial> "<-" <cause: Polynomial> =>?
        Ok(ThinArrowRule::new().with_dots(dots)?.with_cause(cause)),

    // effect-then-cause rule
    <dots: Polynomial> "->" <effect: Polynomial> "<-" <cause: Polynomial> =>?
        Ok(ThinArrowRule::new().with_dots(dots)?.with_cause(cause).with_effect(effect)),

    // cause-then-effect rule
    <dots: Polynomial> "<-" <cause: Polynomial> "->" <effect: Polynomial> =>?
        Ok(ThinArrowRule::new().with_dots(dots)?.with_cause(cause).with_effect(effect)),

    // forward rule
    <cause: Polynomial> "->" <dots: Polynomial> "->" <effect: Polynomial> =>?
        Ok(ThinArrowRule::new().with_dots(dots)?.with_cause(cause).with_effect(effect)),

    // backward rule
    <effect: Polynomial> "<-" <dots: Polynomial> "<-" <cause: Polynomial> =>?
        Ok(ThinArrowRule::new().with_dots(dots)?.with_cause(cause).with_effect(effect)),
};

// fat_arrow_rule = polynomial ( "=>" | "<=" | "<=>" ) polynomial { ( "=>" | "<=" | "<=>" ) polynomial } ;
pub FatArrowRule: FatArrowRule =
    <head: Polynomial> <tail: (<FatOp> <Polynomial>)+> =>
        FatArrowRule::from_parts(head, tail);

// Polynomial

// polynomial = [ [ polynomial ] "+" ] poly_term { poly_term } ;
pub Polynomial: Polynomial = {
    AddOp? <head: PolyTerm> <tail: PolyTerm*> => head.with_product_multiplied(tail),
    <head: Polynomial> AddOp <tail: PolyTerm+> => head.with_product_added(tail),
};

// poly_term = identifier | "(" polynomial ")" ;
#[inline]
PolyTerm: Polynomial = {
    <Identifier> => Polynomial::from(DotName::from(<>)),
    "(" <Polynomial> ")",
};

// Dot list

// dot_list = identifier { identifier } ;
pub DotList: Polynomial =
    <head: DotListTerm> <tail: DotListTerm*> => head.with_product_multiplied(tail);

#[inline]
DotListTerm: Polynomial =
    <Identifier> => Polynomial::from(DotName::from(<>));

// Operators

AddOp: BinOp = "+" => BinOp::Add;
ThinOp: BinOp = { "->" => BinOp::ThinTx, "<-" => BinOp::ThinRx };
FatOp: BinOp = { "=>" => BinOp::FatTx, "<=" => BinOp::FatRx, "<=>" => BinOp::FatDx };

// Identifiers

IdentifierList: Vec<String> = <Identifier+>;

Identifier: String = r"[A-Za-z_][A-Za-z0-9_-]*" => <>.to_owned();

// Literals

SizeList: Vec<Literal> = <Size+>;

FiniteSize: Literal = r"[0-9]+" =>? Ok(Literal::from_digits(<>)?);
Omega: Literal = r"[Ωω]" => Literal::omega();
Theta: Literal = r"[Θθ]" => Literal::theta();
Size: Literal = { FiniteSize, Omega };
Name: Literal = r#""[^"]*""# =>? Ok(Literal::from_quoted_str(<>)?);

extern {
    type Location = usize;
    type Error = AscesisError;

    enum Token<'input> {
        r"///.*\n" => Token::DocComment(<&'input str>),
        r"[A-Za-z_][A-Za-z0-9_-]*" => Token::Identifier(<&'input str>),
        r"[0-9]+" => Token::LiteralFiniteSize(<&'input str>),
        r#""[^"]*""# => Token::LiteralName(<&'input str>),
        r"[Ωω]" => Token::Omega,
        r"[Θθ]" => Token::Theta,
        ";" => Token::Semicolon,
        "," => Token::Comma,
        "." => Token::Dot,
        ":" => Token::Colon,
        "{" => Token::OpenCurly,
        "}" => Token::CloseCurly,
        "(" => Token::OpenParen,
        ")" => Token::CloseParen,
        "[" => Token::OpenBracket,
        "]" => Token::CloseBracket,
        "+" => Token::Add,
        "->" => Token::ThinArrow,
        "<-" => Token::ThinBackArrow,
        "=>" => Token::FatArrow,
        "<=" => Token::FatBackArrow,
        "<=>" => Token::FatTwowayArrow,
        "!" => Token::Bang,
        "ces" => Token::Ces,
        "vis" => Token::Vis,
        "sat" => Token::Sat,
        "caps" => Token::Caps,
        "unbounded" => Token::Unbounded,
        "weights" => Token::Weights,
        "inhibit" => Token::Inhibit,
        "activate" => Token::Activate,
        "drop" => Token::Drop,
    }
}
