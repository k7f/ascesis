<CesFile> ::= <CesFileBlock>
            | <CesFileBlock> <CesFile> ;

<CesFileBlock> ::= <ImmediateDef>
                 | <PropBlock>
                 | <CapsBlock>
                 | <UnboundedBlock>
                 | <WeightsBlock>
                 | <InhibitBlock>
                 | <ActivateBlock>
                 | <DropBlock> ;

;; Structure definition, immediate form

<ImmediateDef> ::= <ImmediateSig> "{" "}"
                 | <ImmediateSig> "{" <Rex> "}" ;

<ImmediateSig> ::= "ces" <Identifier> ;

;; Structure instantiation

<CesImmediate> ::= <Identifier> "(" ")" ;

<CesInstance> ::= <Identifier> "!" "(" ")"
                | <Identifier> "!" "(" <InstanceArgs> ")" ;

<InstanceArgs> ::= <PlainInstanceArgs>
                 | <PlainInstanceArgs> "," ;

<PlainInstanceArgs> ::= <ArgValue>
                      | <ArgValue> "," <PlainInstanceArgs> ;

<ArgValue> ::= <Identifier> ;

;; Presentation context

<PropSelector> ::= "vis" | "sat" ;

<PropBlock> ::= <PropSelector> "{" "}"
              | <PropSelector> "{" <PropList> "}" ;

<PropList> ::= <PlainPropList>
             | <PlainPropList> "," ;

<PlainPropList> ::= <PropField>
                  | <PropField> "," <PlainPropList> ;

<PropField> ::= <Identifier> ":" <PropValue> ;

<PropValueArray> ::= <PlainPropValueArray>
                   | <PlainPropValueArray> "," ;

<PlainPropValueArray> ::= <PropValue>
                        | <PropValue> "," <PlainPropValueArray> ;

<PropValue> ::= <Name>
              | <SizeList>
              | <DotList>
              | "[" <PropValueArray> "]"
              | "{" <PropList> "}" ;

;; Capacities

<CapsBlock> ::= "caps" "{" "}"
              | "caps" "{" <CapList> "}" ;

<CapList> ::= <PlainCapList>
            | <PlainCapList> "," ;

<PlainCapList> ::= <CapField>
                 | <CapField> "," <PlainCapList> ;

<CapField> ::= <Size> <DotList> ;

<UnboundedBlock> ::= "unbounded" "{" "}"
                   | "unbounded" "{" <DotList> "}" ;

;; Weights

<WeightsBlock> ::= "weights" "{" "}"
                 | "weights" "{" <WeightList> "}" ;

<WeightList> ::= <PlainWeightList>
               | <PlainWeightList> "," ;

<PlainWeightList> ::= <WeightField>
                    | <WeightField> "," <PlainWeightList> ;

<WeightField> ::= <Size> <DotList> "->" <DotList>
                | <Size> <DotList> "<-" <DotList>;

;; Inhibitors

<InhibitBlock> ::= "inhibit" "{" "}"
                 | "inhibit" "{" <InhibitList> "}" ;

<InhibitList> ::= <PlainInhibitList>
                | <PlainInhibitList> "," ;

<PlainInhibitList> ::= <InhibitField>
                     | <InhibitField> "," <PlainInhibitList> ;

<InhibitField> ::= <DotList> "->" <DotList>
                 | <DotList> "<-" <DotList>;

;; Activators (zero-weight splits)

<ActivateBlock> ::= "activate" "{" "}"
                  | "activate" "{" <ActivateList> "}" ;

<ActivateList> ::= <PlainActivateList>
                 | <PlainActivateList> "," ;

<PlainActivateList> ::= <ActivateField>
                      | <ActivateField> "," <PlainActivateList> ;

<ActivateField> ::= <DotList> "->" <DotList> ;

<DropBlock> ::= "drop" "{" "}"
              | "drop" "{" <DropList> "}" ;

<DropList> ::= <PlainDropList>
             | <PlainDropList> "," ;

<PlainDropList> ::= <DropField>
                  | <DropField> "," <PlainDropList> ;

<DropField> ::= <DotList> "<-" <DotList> ;

;; Rule expression

<Rex> ::= <ThinArrowRule>
        | <FatArrowRule>
        | <RexTermSeq> ;

<RexTermSeq> ::= <RexTerm>
               | <RexTerm> <RexTermSeq>
               | <RexTerm> "+" <RexTermSeq> ;

<RexTerm> ::= <CesImmediate>
            | <CesInstance>
            | "{" <Rex> "}" ;

;; Arrow rules

<ThinArrowRule> ::= <E-Rule> | <C-Rule> | <EC-Rule> | <CE-Rule> | <FW-Rule> | <BW-Rule> ;

; effect polynomial with explicit dot list on the left
<E-Rule> ::= <DotList> "->" <Polynomial> ;

; cause polynomial with explicit dot list on the left
<C-Rule> ::= <DotList> "<-" <Polynomial> ;

; effect-then-cause polynomial with explicit dot list on the left
<EC-Rule> ::= <DotList> "->" <Polynomial> "<-" <Polynomial> ;

; cause-then-effect polynomial with explicit dot list on the left
<CE-Rule> ::= <DotList> "<-" <Polynomial> "->" <Polynomial> ;

; cause-then-effect pair of polynomials with explicit dot list in the
; middle
<FW-Rule> ::= <Polynomial> "->" <DotList> "->" <Polynomial> ;

; effect-then-cause pair of polynomials with explicit dot list in the
; middle
<BW-Rule> ::= <Polynomial> "<-" <DotList> "<-" <Polynomial> ;

; multi-polynomial rule with implicit dot lists
<FatArrowRule> ::= <Polynomial> <FatArrow> <FatArrowRuleTail> ;

<FatArrowRuleTail> ::= <Polynomial>
                     | <Polynomial> <FatArrow> <FatArrowRuleTail> ;

<FatArrow> ::= "=>" | "<=" | "<=>" ;

;; Polynomial

<Polynomial> ::= <PolyTermSeq>
               | "+" <PolyTermSeq>
               | <Polynomial> "+" <PolyTermSeq> ;

<PolyTermSeq> ::= <PolyTerm>
                | <PolyTerm> <PolyTermSeq> ;

<PolyTerm> ::= <Identifier>
             | "(" <Polynomial> ")" ;

;; Dot list

<DotList> ::= <Identifier>
            | <Identifier> <DotList> ;

;; Identifiers

; FIXME
<Identifier> ::= "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m"
               | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" ;

;; Literals

<SizeList> ::= <Size>
             | <Size> <SizeList> ;

; FIXME
<Size> ::= "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;

; FIXME
<Name> ::= "\"A\"" | "\"B\"" | "\"C\"" | "\"D\"" | "\"E\"" | "\"F\"" | "\"G\"" | "\"H\"" | "\"I\""
         | "\"J\"" | "\"K\"" | "\"L\"" | "\"M\"" | "\"N\"" | "\"O\"" | "\"P\"" | "\"Q\"" | "\"R\""
         | "\"S\"" | "\"T\"" | "\"U\"" | "\"V\"" | "\"W\"" | "\"X\"" | "\"Y\"" | "\"Z\"" ;
